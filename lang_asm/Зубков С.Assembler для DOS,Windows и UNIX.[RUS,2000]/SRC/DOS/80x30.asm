; 80x30.asm
; переводит экран в текстовый режим 80x30 (размер символов 8x16)
; (Norton Commander 5.0 в отличие от, например, FAR восстанавливает режим по 
; окончании программы, но его можно обмануть, если предварительно нажать
; Alt-F9)
;
; Компиляция:
; TASM:
; tasm /m 80x30.asm
; tlink /t /x 80x30.obj
; MASM:
; ml /c 80x30.asm
; link 80x30.obj,,NUL,,,
; exe2bin 80x30.exe 80x30.com
; WASM:
; wasm 80x30.asm
; wlink file 80x30.obj form DOS COM
;

		.model tiny
		.code
		.186		; для команды outsw
		org	100h	; COM-программа
start:
		mov	ax,3	; установить режим 03h (80x25),
		int	10h	; чтобы только внести небольшие изменения

		mov	dx,3CCh	; порт 3CCh: регистр вывода (MOR) на чтение
		in	al,dx
		mov	dl,0C2h	; порт 03C2h: регистр вывода (MOR) на запись
		or	al,0C0h	; установить полярности 1, 1 - для 480 строк
		out	dx,al

		mov	dx,03D4h		; DX = порт 03D4h: индекс CRT
		mov	si,offset crt480	; DS:SI = адрес таблицы данных для CRT
		mov	cx,crt480_l		; CX = ее размер
		rep outsw	; послать все устанавливаемые параметры в порты
				; 03D4h и 03D5h

; нельзя забывать сообщать BIOS об изменениях в видеорежиме
		push	0040h
		pop	es			; ES = 0040h
		mov	byte ptr es:[84h],29	; 0040h:0084h - число строк

		ret

; данные для контроллера CRT в формате индекс в младшем байте, данные в
; старшем - для записи при помощи команды outsw
crt480		dw	0C11h	; регистр 11h всегда надо записывать первым, так 
				; как его бит 7 разрешает запись в другие регистры
		dw	0B06h,3E07h,0EA10h,0DF12h,0E715h,0416h
crt480_l = ($-crt480)/2
		end start
